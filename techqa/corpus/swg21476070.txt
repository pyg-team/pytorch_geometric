Title: IBM Computing the mean and covariance of linear combinations - United States

Text:
 TECHNOTE (TROUBLESHOOTING)

PROBLEM(ABSTRACT)
 I have the mean vector and covariance matrix from a multivariate distribution. I wish to now obtain the mean and variance of a linear combination of the univariate distributions. I know there are formulas for doing this using some basic theorems of linear algebra, namely given C(1) C(2) .. C(n) as coefficients of the linear combination (L). In general I know the following is true:

E(L) = C(1) * Mu(1) + C(2) * Mu(2) + ... + C(n) * Mu(n). 

Var(L) = C(1)**2 * VAR(1) + C(2)**2 * VAR(2) + ... + C(n)**2 * VAR(n).

Is there any simple way to create these formulas in SPSS? 

RESOLVING THE PROBLEM
The simplest way to do this is to use the MATRIX procedure. The necessary code to create the matrix materials and the required linear combinations is given below. This is not limited to a single linear combination; if more than one column for L is provided then one will end up with NEWSIGMA as a square matrix with Covariances of the L's in the off-diagonal elements. 

The following formulas are used in this implimentation: 

For Y ~ (Mu, Sigma), L'Y ~ ( L'* Mu, L'* Sigma * L ) . 

The program is implimented as a MACRO which takes 3 file names as its arguments. These files should contain the Coefficients, the Mean Vector and Covariance Matrix respectively . 

Define LINCOMB ( Covmat !CHAREND('/') 
/ Means !CHAREND('/') 
/ Coefs !CHAREND('/') ) . 
MATRIX . 
GET COVS / FILE = !COVMAT . 
GET COEF / FILE = !Coefs . 
GET MEAN / FILE = !MEANS . 
COMPUTE NEWMEAN = T(COEF)*MEAN . 
COMPUTE NEWCOV = T(COEF) * COVS * COEF . 
COMPUTE SDDIAG = MDIAG(DIAG(NEWCOV)&**-.5). 
COMPUTE NEWCORR = SDDIAG * NEWCOV * SDDIAG . 
PRINT NEWMEAN. 
PRINT NEWCOV . 
PRINT NEWCORR . 
END MATRIX . 
!ENDDEFINE . 

** Example of how to set up files and invoke the Macro. 

* Define Coefficients ( A single column for each linear combination) . 
* In this example L1 = 2 * Y1 - 1 * Y2 + 3 * Y3 
L2 = 1 * Y1 + 0 * Y2 - 1 * Y3 = Y1 - Y3 . 
DATA LIST FREE / L1 L2 . 
BEGIN DATA 
2 1 
-1 0 
3 -1 
END DATA . 
SAVE OUTFILE 'COEF'. 

* Define the Mean Vector * 
DATA LIST FREE / MEAN . 
BEGIN DATA . 
2 1 3 
END DATA . 
SAVE OUTFILE 'MEANS' . 

* Define the Covariance Matrix * 
DATA LIST FREE / COV1 COV2 COV3 . 
BEGIN DATA 
4 -2 0 
-2 1 7 
0 7 5 
END DATA . 
SAVE OUTFILE 'COVAR' . 
SET MPRINT ON . 

LINCOMB Covmat = 'COVAR' / Means = 'Means' / Coefs = 'Coef' . 

* The Macro generates the following results : 

NEWMEAN NEWCOV NEWCORR 
12 28 2 1.000000000 .125988158 
-1 2 9 .125988158 1.00000000

RELATED INFORMATION
 Need more help? Our Statistics forum is Live! [https://developer.ibm.com/answers/topics/statistics.html?smartspace=predictive-analytics]


 

HISTORICAL NUMBER
 14552